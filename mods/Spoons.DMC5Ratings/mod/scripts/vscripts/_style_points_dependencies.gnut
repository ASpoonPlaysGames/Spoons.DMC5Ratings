global function CheckDependencies

// this is an array of all the available songs
// MATCHES THE ONE IN cl_style_points.gnut
global array<string> dmc5SongArray = [ 
    "Silver Bullet",
    "(WIP)Bury The Light",
    "The Duel"
]

struct
{
    string currentMod
    string currentDependency
    string currentURL
} file

void function CheckDependencies()
{
    #if STYLE_POINTS_HAS_KILL_CALLBACKS
       
    #elseif UI
        StylePoints_CreateDependencyDialog( "Spoons.DMC5Ratings", "S2.ClientKillCallback", "https://northstar.thunderstore.io/package/S2Mods/ClientKillCallback/" )
    #endif

    #if STYLE_POINTS_HAS_MOD_SETTINGS
        AddConVarSettingEnum("dmc5_song", "Song", "Devil May Cry 5 Ratings", dmc5SongArray)
    #elseif UI
        StylePoints_CreateDependencyDialog( "Spoons.DMC5Ratings", "Mod Settings", "https://northstar.thunderstore.io/package/EladNLG/ModSettings/" )
    #endif
}

void function StylePoints_CreateDependencyDialog( string mod, string dependency, string url )
{
    file.currentMod = mod
    file.currentDependency = dependency
    file.currentURL = url
    DialogData dialogData
    dialogData.header = Localize("#MISSING_DEPENDENCY_HEADER")
	
    array<string> mods = NSGetModNames()
    // mod is installed but disabled
    if ( mods.contains( dependency ) && !NSIsModEnabled( dependency ) )
    {
        dialogData.message = Localize( "#MISSING_DEPENDENCY_BODY_DISABLED", mod, dependency )

	    AddDialogButton( dialogData, Localize("#ENABLE_MOD", dependency), StylePoints_EnableMod )
        AddDialogButton( dialogData, Localize("#DISABLE_MOD", mod), StylePoints_DisableMod )
        AddDialogFooter( dialogData, "#A_BUTTON_SELECT" )
	    AddDialogFooter( dialogData, "#B_BUTTON_BACK" )
    }
    else
    {
        dialogData.message = Localize( "#MISSING_DEPENDENCY_BODY_INSTALL", mod, dependency, url )

	    AddDialogButton( dialogData, "#OPEN_THUNDERSTORE", StylePoints_InstallMod )
        AddDialogButton( dialogData, Localize("#DISABLE_MOD", mod), StylePoints_DisableMod )
        AddDialogFooter( dialogData, "#A_BUTTON_SELECT" )
	    AddDialogFooter( dialogData, "#B_BUTTON_BACK" )
    }

	OpenDialog( dialogData )
}

void function StylePoints_EnableMod()
{
    NSSetModEnabled( file.currentDependency, true )
    ReloadMods()
}

void function StylePoints_InstallMod()
{
    LaunchExternalWebBrowser(file.currentURL, WEBBROWSER_FLAG_FORCEEXTERNAL)
    ReloadMods()
}

void function StylePoints_DisableMod()
{
    NSSetModEnabled( file.currentMod, false )
    ReloadMods()
}